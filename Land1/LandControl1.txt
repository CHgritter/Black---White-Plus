challenge LandControl1

begin script LandControl1

 dyk76 = marker at [1751.1938, 0.0000, 2466.3926]				// fish farms
 MyCitadel = 0
 CreatureGates = 0
 CreatureActive = 0

start
	run script SetupLand1
		run background script SingingStoneCircle							// Places Stones and wait until SingingStonesStart == 1

	if IsSkippingToCreatureSelect == 0
		run script FollowUs
		run script CitadelGuide
	else
		build building at [1915.05,2508.89] desire 1.0
		MyCitadel = get CITADEL at [1915.05,2508.89] radius 5
		BUILT of MyCitadel = 1
	end if

	run background script DidYouKnow(dyk76,variable HELP_TEXT_DYK_76, variable DYK_VILLAGE_LIFE)			// fish farms

		run background script ThrowingStones
		ThrowingStonesStart = 1
		run background script TheLostFlock
	
	if IsSkippingToCreatureSelect == 0
		run script ChooseYourCreature
	else
		GateKey1 = get OBJECT MOBILE_STATIC_INFO_GATE_TOTEM_APE at [LostBrotherHouse] radius 20
		delete GateKey1
		GateKey2 = get OBJECT MOBILE_STATIC_INFO_GATE_TOTEM_TIGER at [FollowUsCrowdPosition] radius 30
		delete GateKey2
		QuarryRock = get OBJECT MOBILE_STATIC_INFO_GATE_TOTEM_BLANK at [QuarryPos] radius 30
		delete QuarryRock
		// Herolich1 DevMode: If devmode is greater than zero and account already has a creature, the creature intro is skipped
		// This is for the vortex option, to get into the game faster. There is no dev mode for the parts prior to this due to it
		// not needing as much work. This is best used when skipping to the next land as fast as possible. (The vortex option.)
		// In normal gameplay, you will always first be brought through the CreaturesInGlade script
		if DevMode <= 0 or HasOldCreature == 0
			run script CreaturesInGlade
		else
			set game time 15.40
			enable game time
			set camera zones to "Land1Zone5.exc"
			CreatureGates = get FEATURE at [1781.03,2810.63]
			open CreatureGates
			KeptOldCreature = 1 // Herolich1: added KeptOldCreature check so Sable quests are skipped when not skipping creature guide.
			ChosenCreature = 0
			ChooseYourCreatureFinished = 1
			load my_creature at [1850.00, 1300.00]  // arbitrary position
		end if
	end if
		run background script RunCreatureBreederLand(1)
		run background script TheMissionaries
		run background script StartingCreatures // Herolich1: a script that places two or all of the starter creatures in the glade.
	if IsSkippingCreatureGuide == 0
		run background script MoveTheGuideAround
	end if

	// Herolich1: Added... a disgusting piece of code asking the player to refresh the gamestate so the creature is correctly found.
	// I hate it. But I have no other choice.
	if KeptOldCreature == 0 and HasOldCreature == 1
		set interaction SCRIPT_INTERFACE_LEVEL_JUST_HAND_MOVE
		begin cinema
			set camera position to [1431.1, 20.5, 2005.5] 
			set camera focus to [1542.8, 11.5, 2180.6]
		end cinema
		set fade in time 2
		while CreatureActive == 0
			wait 3 seconds
			MyCreature = get player 1 creature
			if MyCreature exists
				CreatureActive = 1
			else
				say single line "Please go into the skirmish menu or quit and reopen the game so your creature gets recognized." //MANUAL DIALOG HELP_TEXT_PLUS_REPLACE_OLD_CREATURE
			end if
		end while
		set fade red 0 green 0 blue 0 time 2
		wait 2 seconds
		set interaction SCRIPT_INTERFACE_LEVEL_NORMAL
	end if

//	run script CreatureDevelopmentSet

	run script CreatureDevSeeHome
	if IsSkippingCreatureGuide == 0 and KeptOldCreature == 0 // Herolich1: added KeptOldCreature check so Sable quests are skipped when not skipping creature guide.
		run script CreatureDevLearnToEat
		run script CreatureDevPunishment
	else
		MyCreature = get player 1 creature
		set MyCreature DEVELOPMENT_PHASE_FULLY_MATURE development
		run SCRIPT_DEV_FUNCTION_ROPE_LEASH_ENABLED developer function
		run SCRIPT_DEV_FUNCTION_OTHER_LEASHES_ENABLED developer function
		disable MyCreature development script
		release MyCreature
	end if
		run background script MagicMushroom
		SingingStonesStart = 1

	if IsSkippingCreatureGuide == 0 and KeptOldCreature == 0 // Herolich1: added KeptOldCreature check so Sable quests are skipped when not skipping creature guide.
		run script CreatureDevLeashIntro
	end if

		run background script HermitMain

	if IsSkippingCreatureGuide == 0 and KeptOldCreature == 0 // Herolich1: added KeptOldCreature check so Sable quests are skipped when not skipping creature guide.
		run script CreatureDevLeashAttachToHouse
	end if

		LostFlockWanted = 1
		run background script CreatureSavingPeople							// This waits until creature big enough
//	run script CreatureGuideSet

	if IsSkippingCreatureGuide == 0 // Herolich1: added an else statement so that the Aztec village can be taken when skipping the creature guide.
		run script MeetTheGuide
		run script GuideAsksToMeetYourCreature
		run script GuideImpressTownLesson
	else
		GuideNotImpressing = 1
	end if

		run background script PiedPiper

	if IsSkippingCreatureGuide == 0 // Herolich1: added an else statement so that Sleg can still be fought can be taken when skipping the creature guide.
		run script GuideImpressTownNextLesson
		run script CreatureDevGuideTeachesFight
	else
		CreatureDevelopment_Fight = 1
	end if

		run background script CreatureGuardian								
		run background script LeaveThroughVortexL1							// This waits until OpenVortexL1 == 1 (set in TheStorm)

	if IsOpeningVortex == 0 // Herolich1: replaced "IsSkippingCreatureGuide" with "IsOpeningVortex"
		run script TheStorm	
	else
		wait until LeaveLandNow == 1
	end if
	run script LeavingLand1Scene

end script LandControl1

