challenge SPIRITUAL_HEALER

//------------------------------------------------------------------------------------------------------------------------
global	SaidSpiritualHealerChildKillCount=0

//------------------------------------------------------------------------------------------------------------------------
// This just keeps all the Town at the right age
//------------------------------------------------------------------------------------------------------------------------
begin script TownAge(Healer, Town)
	HealSpell = 0

start
	begin loop

		if camera position near [Healer] radius 100 and Healer viewed
			start say sound HELP_TEXT_SPIRITUALHEALER_19 at [Healer]// While my heart beats you will all never age!
		end if

		AGE of Town = 16
		if Healer is not HELD and Healer not in MyCreature hand
			HealSpell = create special effect SPOT_VISUAL_COMMAND_SUCCEED at [Healer] time 3
		end if
		wait 3*60 seconds

	until HEALTH of Healer <= 0

	end loop

end script TownAge

//------------------------------------------------------------------------------------------------------------------------
// Move Healer around Town
//------------------------------------------------------------------------------------------------------------------------
begin script Walkabout(Healer)
	Point1 = marker at [1998.0679, 15.4100, 2566.9585]
	Point2 = marker at [2037.6305, 14.2454, 2559.8923]
	Point3 = marker at [2050.6636, 15.4100, 2587.1479]
	Point4 = marker at [2053.9617, 15.2006, 2562.9470]
	Point5 = marker at [2034.4944, 15.4362, 2605.9011]

start
	begin loop

		move Healer position to [Point1]
		wait until [Healer] near [Point1] radius 5 or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

		Healer play ANM_P_LOOKING_FOR_SOMETHING loop 1
		wait until Healer played or 30 seconds

		move Healer position to [Point2]
		wait until [Healer] near [Point2] radius 5 or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

		Healer play ANM_P_AMBIENT1 loop 1
		wait until Healer played or 30 seconds

		move Healer position to [Point4]
		wait until [Healer] near [Point4] radius 5 or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

		Healer play ANM_P_AMBIENT1 loop 1
		wait until Healer played or 30 seconds

		move Healer position to [Point3]
		wait until [Healer] near [Point3] radius 5 or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

		Healer play ANM_P_LOOKING_FOR_SOMETHING loop 1
		wait until Healer played or 30 seconds

		move Healer position to [Point5]
		wait until [Healer] near [Point5] radius 5 or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

		Healer play ANM_P_AMBIENT2 loop 1
		wait until Healer played or 30 seconds
		wait until Healer is not HELD and Healer not in MyCreature hand

	until HEALTH of Healer <= 0

	end loop
		

end script Walkabout

//------------------------------------------------------------------------------------------------------------------------
begin script KillChild(Healer, Town, Child)

	Crime = 0
	LifeForce = 0

start

	Crime = marker at [Healer]

	stop script "Walkabout"

	move Healer position to [Crime]
	
	detach Child from Town

	set Healer focus to [Child]

	Healer play ANM_P_BECKON loop 1

	LifeForce = create special effect SPOT_VISUAL_MAGIC_BEAM on Healer time 20
	add LifeForce target on Child

	move Child position to [Healer] 
	wait until [Child] near [Healer] radius 1.5 or 30 seconds

	set Child position to [Child]

	set Child focus to [Healer]

	if SaidSpiritualHealerChildKillCount <= 1
		if camera position near [Healer] radius 150 and Healer viewed and widescreen ready
			
			begin dialogue

				eject good spirit
				//"Oh, I dont think I wanna see this !"
				say HELP_TEXT_SPIRITUALHEALER_23
				wait until read

				make good spirit disappear

				eject evil spirit
				//"I do !, Let me have a look!"
				say HELP_TEXT_SPIRITUALHEALER_24
				wait until read

			end dialogue
		end if
		SaidSpiritualHealerChildKillCount++
	end if

	delete LifeForce

	// Healer play ANM_S_PRIEST_SUCK_LIFE_FORCE loop 1

	Healer play ANM_P_BLOW_RASPBERRY loop 1

	if camera position near [Healer] radius 150 and Healer viewed
		start say sound HELP_TEXT_SPIRITUALHEALER_18 at [Healer]//	I take this life to feed the life of the many!
	end if

	//Healer play ANM_S_PRIEST_SUCK_LIFE_FORCE loop 1
	wait until Healer played

	HEALTH of Child = 0

	release Child

	Healer play ANM_P_CROWD_IMPRESSED_1 loop 1

	wait 6 seconds

	run background script Walkabout(Healer)

end script KillChild

//------------------------------------------------------------------------------------------------------------------------
begin script HealerKilledOrBanished(Healer, Town, IsKilled)

start
	
	stop script "Walkabout, KillChild"

	if camera position near [Healer] radius 100 and Healer viewed or IsKilled == 2
		begin cinema
			set camera position to [Healer] + [5,5,5]
			set camera focus to [Healer]

			move camera position to [Healer] + [20,20,20] time 6

			if IsKilled == 1
				say HELP_TEXT_SPIRITUALHEALER_22		// ARGH !!
				wait until read
			elsif IsKilled == 2 // Not banished or dead, but there's no one left.
				say single line "The Healer: Is this the reward I get for my efforts?"//MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_13
				wait until read

				say single line "The Healer: Fine then! Have it your way!"//MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_14
				wait until read
				
				attach Healer to get town with id 2
				release Healer
			else
				say HELP_TEXT_SPIRITUALHEALER_16		//That's it it's all over!
				wait until read
				
				attach Healer to get town with id 2
				release Healer
			end if

			wait until camera ready

			set fade red 0 green 0 blue 0 time 2
			wait until fade ready

			set camera position to [2061.5562, 21.5429, 2583.5093]
			set camera focus to [2020.8864, 15.5401, 2604.3081]

			set fade in time 2
			wait until fade ready

			if IsKilled == 2
				run background script BurnArea(marker at [Town], 150, 0.2)
				wait 2 seconds
			else
				HEALTH of Town = 0
			end if

			if IsKilled == 1
				say single line HELP_TEXT_SPIRITUALHEALER_20 // You kill me, you kill them all.
				wait until read
 			end if

			//Well you stopped those sacrifices
			if IsKilled == 2
				say single line "Good Spirit: Well, at least no further lives have been lost..." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_15
				wait 8 seconds
			else
				say single line HELP_TEXT_SPIRITUALHEALER_17
				wait until read and 10 seconds
			end if

		end cinema
	else 
		HEALTH of Town = 0
	end if
	if KhazarGone == 0
		if IsKilled == 2
			run script ComputerPlayerConfrontPlayer(2, variable "Don't fret, my friend. You've done the right thing.", 2) // MANUAL DIALOG HELP_TEXT_PLUS_GOD_COMMENT_SPIRITUALHEALER_03
		else
    		run script ComputerPlayerConfrontPlayer(2, variable "It looks as though this was no bluff...", 2) // MANUAL DIALOG HELP_TEXT_PLUS_GOD_COMMENT_SPIRITUALHEALER_02
		end if
	end if
	if LethysGone == 0
	    run script ComputerPlayerConfrontPlayer(3, variable "If I were you, I would have let him be. But let me guess: you work in mysterious ways!", 2) // MANUAL DIALOG HELP_TEXT_PLUS_GOD_COMMENT_SPIRITUALHEALER_04
	end if
end script HealerKilledOrBanished

//------------------------------------------------------------------------------------------------------------------------
// Herolich1: Burn the town down, made by Daniels118
//------------------------------------------------------------------------------------------------------------------------
begin script BurnArea(Position, Radius, Speed)
	Cx = SCRIPT_OBJECT_PROPERTY_TYPE_XPOS of Position
	Cz = SCRIPT_OBJECT_PROPERTY_TYPE_ZPOS of Position
	XStep = 10
	ZStep = XStep * 0.757 //0.707
	CellRadius = XStep / 2
	CurrOffset = 0
	x0 = Cx - Radius + CellRadius
	z0 = Cz - Radius + CellRadius
	x1 = Cx + Radius - CellRadius
	z1 = Cz + Radius - CellRadius
	X = x0
	Z = z0
	Building = 0
start
	//@noyield
	while X < x1
		Z = z0 + CurrOffset
		//@noyield
		while Z < z1
			Building = get SCRIPT_OBJECT_TYPE_ABODE at [X, 0, Z] radius CellRadius
			if Building exists
				enable Building on fire Speed
			end if
			Z += ZStep
		end while
		X += XStep
		CurrOffset = CellRadius - CurrOffset
	end while
end script BurnArea
//------------------------------------------------------------------------------------------------------------------------
begin script FirstTimeBanished(Healer, Town)
start
	begin dialogue
		start say sound HELP_TEXT_SPIRITUALHEALER_21		// Banish me and they die !
 		wait until read
	end dialogue
end script FirstTimeBanished

//------------------------------------------------------------------------------------------------------------------------
// Herolich1: Intro Cutscene
//------------------------------------------------------------------------------------------------------------------------
begin script SpiritualHealerIntro(HealerHut, Town)
	Highlight = 0
	IntroVillager = 0
	VillagerStartPos = marker at [2086.6,22.9,2569.5]

	CamPos1 = marker at [2106.8,26.2,2565.6]
	CamFoc1 = marker at [2028.5,15.5,2571.0]
	CamPos2 = marker at [2128.8,54.1,2561.0]
	CamFoc2 = marker at [2037.6,15.6,2573.1]

	KhazarPosition = marker at [2027.0,35.4,2562.5]
start 
	Highlight = create highlight HIGHLIGHT_CHALLENGE at [HealerHut]
	run script ChallengeHighlightNotify(Highlight, HealerHut, variable GOOD_ADVISOR, variable HELP_TEXT_GENERAL_CHALLENGE_START_06)

	IntroVillager = create VILLAGER VILLAGER_INFO_GREEK_FARMER_MALE at [VillagerStartPos]
	KhazarInScript=1

	begin cinema
		start music MUSIC_TYPE_SCRIPT_GREGORIAN
		enable IntroVillager high graphics detail
		IntroVillager play ANM_P_PRAY loop -1

		eject evil spirit
		say single line HELP_TEXT_DROWNINGFISHERMEN_01 //What's all the fuss?
		move camera position to [CamPos1] time 4
		move camera focus to [CamFoc1] time 4
		wait until read
		send evil spirit home

		wait until camera ready
		set IntroVillager focus to camera position
		say single line "Greek dude: Holy one, there's a stranger in our lands capable of miracles, but at a terrible price." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_01
		wait until read

		IntroVillager play ANM_P_TALK1_MALE loop -1
		say single line "Greek dude: Ever since he arrived, no one in our village has aged a moment. But it's at the cost of our children!" //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_02
		wait until read

		IntroVillager play ANM_P_MOURNING loop -1
		say single line "Greek dude: Every day he calls forward a child to sacrifice for the many..." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_03
		wait until read

		say single line "Greek dude: None of us asked for this. Please, do something!" //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_04
		wait until read

		move camera position to [CamPos2] time 5
		move camera focus to [CamFoc2] time 5

		wait until camera ready
		eject good spirit
		say single line "Good spirit: How appalling! We must stop this at once!" //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_05
		wait until read

		eject evil spirit
		say single line "Evil spirit: Kill him! Throw him off a cliff, yeah!" //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_06
		wait until read

		send good spirit home
		send evil spirit home
		disable IntroVillager high graphics detail 
		delete IntroVillager with fade
		if KhazarGone == 0
			move computer player 2 to [KhazarPosition] speed 350 with fixed height
			wait until computer player 2 ready
			if PLAYER of Town == 1
				say single line "Khazar: An immortal town, at a terrible cost... I'll leave the solution to you." with interaction //MANUAL DIALOG HELP_TEXT_PLUS_GOD_COMMENT_SPIRITUALHEALER_01
			else
				say single line "Khazar: This situation is a conundrum... Don't worry, my ally. I will handle it... somehow." with interaction //MANUAL DIALOG HELP_TEXT_PLUS_GOD_COMMENT_SPIRITUALHEALER_06
			end if
			wait until read
			release computer player 2
		end if
		stop music
	end cinema
	KhazarInScript=0
end script SpiritualHealerIntro


//------------------------------------------------------------------------------------------------------------------------
// Herolich1: Your Conciousness discuss the issue
//------------------------------------------------------------------------------------------------------------------------
begin script SpiritualHealerAfterDroppedFirstTime
	DialoguePlayed=0
start 
	if DialoguePlayed == 0
		wait 2 seconds
		DialoguePlayed = 1
		begin dialogue
			eject evil spirit
			say single line "Evil Spirit: I love carnage, but I don't think it's worth an entire town." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_07
			wait until read

			say single line "Evil Spirit: Although... we don't really need a middle man for this." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_08
			wait until read

			eject good spirit
			say single line "Good Spirit: Out of the question! Surely we can find a way around it." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_09
			wait until read

			say single line "Good Spirit: What if we moved everyone out of this town instead? We might be able to break the Healer's curse with it!" //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_10
			wait until read

			say single line "Evil Spirit: That sounds boring to me." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_11
			wait until read

			say single line "Good Spirit: Of course it does." //MANUAL DIALOG HELP_TEXT_PLUS_SPIRITUALHEALER_12
			wait until read

			send good spirit home
			send evil spirit home
		end dialogue
	end if
end script SpiritualHealerAfterDroppedFirstTime

//------------------------------------------------------------------------------------------------------------------------
// Landed in town - not entry is in camera mode follow.
//------------------------------------------------------------------------------------------------------------------------
begin script LandedSafeInTown(Healer, Town)
start 
	begin dialogue
		start say sound HELP_TEXT_SPIRITUALHEALER_20 // You kill me, you kill them all.
		wait until read
	end dialogue
end script LandedSafeInTown

//------------------------------------------------------------------------------------------------------------------------
begin script SpiritualHealerMain
	Healer = 0
	HealerHut = get HOUSE at [2010.1100, 15.4100, 2603.3501]
	HealerPos = marker at [2017.1619, 15.4100, 2594.7529]
	Town = get town with id 9
	DialoguePlayed=0

	Child = 0

	Finished = 0
	Banished = 0

	Villager1 = 0
	Villager2 = 0
	Villager3 = 0
	Villager4 = 0

start

	Healer = create VILLAGER PRIEST at [HealerHut]

	run background script Walkabout(Healer)
	run background script TownAge(Healer, Town)
	run background script SpiritualHealerIntro(HealerHut, Town)

	while Finished == 0
		begin loop
			wait 30 seconds
			Child = get CHILD in Town

			if Child exists
				run script KillChild(Healer, Town, Child)
			end if

		until Healer is HELD or Healer in MyCreature hand or HEALTH of Healer <= 0 or not get VILLAGER in Town exists
		end loop

		stop script "Walkabout, KillChild"
		
		if get VILLAGER in Town exists
			if HEALTH of Healer > 0
				start say sound HELP_TEXT_SPIRITUALHEALER_15 at [Healer] //Put me down I'm needed
			end if

			wait until Healer is not HELD and not Healer in MyCreature hand

			if HEALTH of Healer > 0
				if Healer is FLYING
					begin cinema
						set camera focus follow Healer
						set	camera position follow Healer
						wait until not Healer is FLYING
					end cinema
				end if

				if Healer exists
					//If we are away from the town.
					if get distance from [Healer] to [Town]	> 200
						if Banished == 0
							run script FirstTimeBanished(Healer, Town)
							Banished =1
							DialoguePlayed = 1
						else
							run script HealerKilledOrBanished(Healer, Town, 0)
							Finished=1
							DialoguePlayed = 2
						end if
					else
						run script LandedSafeInTown(Healer, Town)
						run background script Walkabout(Healer)
						DialoguePlayed = 1
					end if
				end if

			else 
				run script HealerKilledOrBanished(Healer, Town, 1)
				Finished=1
				DialoguePlayed = 2
			end if

			if DialoguePlayed == 1
				run script SpiritualHealerAfterDroppedFirstTime
				DialoguePlayed = 2
			end if
		else
			run script HealerKilledOrBanished(Healer, Town, 2) //added a third situation
			Finished=1
		end if
	end while

	wait until 3*60 seconds

	Villager1 = create VILLAGER VILLAGER_INFO_NORSE_FARMER_MALE at [HealerHut]
	Villager2 = create VILLAGER VILLAGER_INFO_NORSE_HOUSEWIFE_FEMALE at [HealerHut]
	Villager3 = create VILLAGER VILLAGER_INFO_NORSE_HOUSEWIFE_FEMALE at [HealerHut]
	Villager4 = create VILLAGER VILLAGER_INFO_NORSE_FORESTER_MALE at [HealerHut]

end script SpiritualHealerMain
