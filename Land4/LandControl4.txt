challenge LandControl4

begin script LandControl4
start

	// Herolich1: TITLE CARD!!
	begin cinema
		set draw text colour red 255 green 255 blue 255
		set clipping window across 0 down 0 width 0 height 0 time 0
		set fade in time 1
		wait until fade ready
		draw text "Land 4" across 0.25 down 0.30 width 0.5 height 0.1 size 0.075 fade in time 2 seconds
		wait 2 seconds
		draw text "The Return" across 0.25 down 0.45 width 0.5 height 0.1 size 0.075 fade in time 2 seconds
		wait 5 seconds
		fade all draw text time 3 seconds
		wait 2 seconds
		set fade red 0 green 0 blue 0 time 2
		wait 2 seconds
		clear clipping window time 0
	end cinema

	run script SetupLand4
	run script BeginLand4(1)
	
	if DevMode >= 5
		run script LeaveThroughVortexL4
	else
		run script SetupUndeadVillage
		run background script SetUndeadTownBeliefToZero
		run background script SetUndeadTownHealth
		run background script ThesiusPuzzleLand4
		run background script JapaneseTotemPuzzle
		run background script FishPuzzle
 		run background script RunCreatureBreederLand(4)

		run background script BuildUpTown

		run background script Land4Ogre	
		run background script Land4Nomad
		run background script Land4Puzzle
		//run script Land4Puzzle

		run background script KeepCreatureAwayFromOgre

		// Japanese man tells you what happened to the land
		run background script ManExplainsAboutNemesis

		run background script BlindWomanMain					// Waits until Aztec town owned

		// TEMP:  For testing only
		//LighteningMeteorBroken	= 1
		//DarknessMeteorBroken	= 1
		//FireMeteorBroken		= 1

		// Wait until all the stones have been destroyed
		wait until LighteningMeteorBroken == 1 and DarknessMeteorBroken == 1 and FireMeteorBroken == 1

		// Start the explanation about the Undead Village
		run script ManExplainsAboutUndeadVillage

		run script CreatureGetsCreed

		run script LeaveThroughVortexL4
	end if
	run script LeavingLand4Scene
end script LandControl4
